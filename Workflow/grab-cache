#!/bin/zsh

readonly cache_repo='vitorgalvao/short-films-cache'
readonly cache_file="${alfred_workflow_cache}/cache.json"
readonly state_file="${alfred_workflow_cache}/state.txt"

# Helper to show results and exit
function show_data {
  < "${cache_file}" && exit 0

  # If cache somehow failed to load, show message and delete state
  echo '{ "items": [{ "title": "Unable to Load Results",
    "subtitle": "Open the debugger and try again",
    "valid": false }] }'
  rm -f "${state_file}"
  exit 1
}

# Get latest state
readonly latest_release="$(curl --silent "https://api.github.com/repos/${cache_repo}/releases/latest")"
readonly download_url="$(osascript -l JavaScript -e 'function run(argv) { return JSON.parse(argv[0])["assets"][0]["browser_download_url"] }' "${latest_release}")"
readonly state="$(osascript -l JavaScript -e 'function run(argv) { return JSON.parse(argv[0])["id"] }' "${latest_release}")"

# Exit early if state has not changed
[[ "${state}" == "$(< "${state_file}")" ]] && show_data

# Update cache and state
mkdir -p "${alfred_workflow_cache}"
curl --silent --location "${download_url}" | tar --directory "${alfred_workflow_cache}" --extract && echo "${state}" > "${state_file}"
show_data
